P2 (Stand) {
  * (Stand)
  | !GotPower ? : ()
  | Cell(F,P) ? Pick(F) : (Stand)
  | Key(i) & Cell(F,T) ? Pop(F,T) : (Stand)
  | Key(i) ? Hit(F)  :(Stand)
  | Key(o) ? Move(N) :(Stand)
  | Key(l) ? Move(S) :(Stand)
  | Key(k) ? Move(W) :(Stand)
  | Key(m) ? Move(E) :(Stand)
  | Key(p) ? Power : (Stand)
  | Key(u) ? Egg(F, D) : (Stand)
  
  *()
 }

P1 (Stand) {
  * (Stand)
  | !GotPower ? : ()
  | Cell(F,P) ? Pick(F) : (Stand)
  | Key(a) & Cell(F,T) ? Pop(F,T) : (Stand)
  | Key(a) ? Hit(F)  :(Stand)
  | Key(z) ? Move(N) :(Stand)
  | Key(s) ? Move(S) :(Stand)
  | Key(q) ? Move(W) :(Stand)
  | Key(d) ? Move(E) :(Stand)
  | Key(e) ? Power : (Stand)
  | Key(r) ? Egg(F, D) : (Stand)
  
  *()
 }

Bush(Existing){
  *(Existing)
  | !GotPower ? 5% Egg(H,A) / 50% Egg(H,P,1) / 5% Egg(H,P,2) : (Growing)
  
  * (Growing)
  | True ? Wait(60000) : (Existing)
 } 
 
 Nothing (Existing){
  *(Existing)
  | True ? : (Existing)
  } 
 
 Bomb(Existing){
 	*(Existing)
 	| True ? Wait(5000) : (Explosion)

 	*(Explosion)
 	| True ? Explode : ()
 	
 	* ()
 }
 
 Projectile(Forward){
   *(Forward)
   | True ? Move : (Forward)
   
   *()
 }
 
MeleeEnnemy(Search) {
   * (Search)
   | !GotPower ? Egg(H,P,1) : ()
   | Cell(F,P) ? Pick : (Search)
   | Cell(@,N) ? Hit(N) : (Search)
   | Cell(@,S) ? Hit(S) : (Search)
   | Cell(@,E) ? Hit(E) : (Search)
   | Cell(@,W) ? Hit(W) : (Search)
   | Closest(@, N) & Cell(N, V)? Move(N) : (Search)
   | Closest(@, W) & Cell(W, V)? Move(W) : (Search)
   | Closest(@, S) & Cell(S, V)? Move(S) : (Search)
   | Closest(@, E) & Cell(E, V)? Move(E) : (Search)
   | True ? 50% Move(F) / 25% Move(R) / 25% Move(L) : (Search)
   
   * ()
 }
 
RangeEnnemy(Search) {
   * (Search)
   | !GotPower ? Egg(H,P,1) : ()
   | Cell(F,P) ? Pick : (Search)
   | Cell(@,N,3) ? Hit(N) : (Search)
   | Cell(@,S,3) ? Hit(S) : (Search)
   | Cell(@,E,3) ? Hit(E) : (Search)
   | Cell(@,W,3) ? Hit(W) : (Search)
   | Closest(@, N) & Cell(N, V)? Move(N) : (Search)
   | Closest(@, W) & Cell(W, V)? Move(W) : (Search)
   | Closest(@, S) & Cell(S, V)? Move(S) : (Search)
   | Closest(@, E) & Cell(E, V)? Move(E) : (Search)
   | True ? 50% Move(F) / 25% Move(R) / 25% Move(L) : (Search)
   
   * ()
 }
 
  Villager (Walking) {
  * (Walking)
  | Cell(@, B) ? Turn(B) : (Standing)
  | Cell(@, L) ? Turn(L) : (Standing)
  | Cell(@, R) ? Turn(R) : (Standing)
  | Cell(@, F) ? : (Standing)
  | True ? 60% Move(F) / 20% Move(L) / 20% Move(R): (Walking)
  
  * (Standing)
  | !Cell(@, F) ? : (Walking)
  }
  
  Rock(Existing){
  *(Existing)
  | !GotPower ? : ()
  
  * ()
  }
  
  Torch(Off){
  *(Off)
  | !GotPower ? Pop : (On)
  
  * (On)
  }
  
  Hermit (Turning) {
  * (Turning)
  | Cell(R, V) ? Turn(R) : (Moving)
  
  * (Moving)
  | Cell(F, V) ? Move(F) : (Turning)
  }
  
  
  Portal (Waiting) {
  	* (Waiting)
  	| True ? 25% Wait(5000) / 25% Wait(10000) / 25% Wait(15000) / 25% Wait(20000) : (Drop)
  	
  	* (Drop)
  	| True ? 25% Egg(F,A) / 25% Egg(R,A) / 25% Egg(L,A) / 25% Egg(B,A) : (Waiting) 
  
  }
  
  Boss(Phase1){
	*(Phase1)
	| !GotPower(470) ? : (Phase2)
	| True ? Hit : (Wait1)
	
	* (Wait1)
	| True ? Wait(15000) : (Phase1)
	
	* (Phase2)
	| !GotPower(20) ? : (Phase3)
	| True ? Pop : (Wait2)
	
	* (Wait2)
	| True ? Wait(5000) : (Phase2)
	
	* (Phase3)
	| !GotPower ? : ()
	| True ? 30% Hit / 35% Pop / 35% Wizz : (Wait3)
	
	* (Wait3)
	| True ? Wait(10000) : (Phase3)
	
	* ()
  }
 
 